s = _{
    " " |
    "\t"|
    "\r"|
    "\n"|
    "\x0b"|
    "\x0c" |
    "--" ~ (!NEWLINE ~ ANY)* ~ NEWLINE |
    "/*" ~ (!"*/" ~ ANY)* ~ "*/"
}

back_quoted     = _{ "`" ~ (!("`"|"\\") ~ ANY | "``" | "\\" ~ ANY)* ~ "`" }
single_quoted   = _{ "'" ~ (!("'"|"\\") ~ ANY | "''" | "\\" ~ ANY)* ~ "'" }
double_quoted   = _{ "\"" ~ (!("\""|"\\") ~ ANY | "\"\"" | "\\" ~ ANY)* ~ "\"" }

balanced = _{
    "(" ~ (balanced|s)* ~ ")" |
    "[" ~ (balanced|s)* ~ "]" |
    "{" ~ (balanced|s)* ~ "}" |
    back_quoted |
    single_quoted |
    double_quoted |
    !("("|"["|"{"|"`"|"'"|"\""|")"|"]"|"}"|s) ~ ANY
}

ident = @{
    back_quoted |
    double_quoted |
    "[" ~ (!"]" ~ ANY)* ~ "]" |
    (ASCII_ALPHA|"_") ~ (ASCII_ALPHANUMERIC|"_")*
}

qname = {
    ident ~ (s* ~ "." ~ s* ~ ident){0,2}
}

column_definition = {
    (!"{{" ~ (balanced|s))+
}

table_options = {
    ANY*
}

create_table_content = {
    "(" ~ (column_definition | "{{" ~ s* ~ expr ~ s* ~ "}}")* ~ ")" ~ table_options
}

create_table = _{
    SOI ~ s* ~ ^"create" ~ s+ ~ ^"table" ~ s* ~ qname ~ s* ~ create_table_content
}

expr = _{
    expr_rownum | expr_null | expr_true | expr_false |
    expr_function |
    expr_string |
    expr_number |
    expr_neg |
    expr_case_value_when |
    expr_variable
}

expr_rownum = { ^"rownum" }
expr_null = { ^"null" }
expr_true = { ^"true" }
expr_false = { ^"false" }
expr_function = {
    qname ~ s* ~ "(" ~ s* ~ (expr ~ s* ~ ("," ~ s* ~ expr ~ s*)*)? ~ ")"
}
expr_string = {
    single_quoted
}
expr_number = @{
    ^"0x" ~ ASCII_HEX_DIGIT+ |
    (ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? | "." ~ ASCII_DIGIT+) ~ (^"e" ~ ("+"|"-")? ~ ASCII_DIGIT+)?
}
expr_neg = {
    "-" ~ s* ~ expr
}
expr_case_value_when = {
    ^"case" ~ s* ~ expr ~ s* ~
    (^"when" ~ s* ~ expr ~ s* ~ ^"then" ~ s* ~ expr ~ s*)+ ~
    (^"else" ~ s* ~ expr ~ s*)? ~
    ^"end"
}
expr_variable = {
    "@" ~ ident ~ (s* ~ ":=" ~ s* ~ expr)?
}
